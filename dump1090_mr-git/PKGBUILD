# Maintainer: anish [ at ] gatech [dot] edu

pkgname=dump1090_mr-git
_gitname=dump1090_mr
pkgver=1.15.r20.g069ebf3
pkgrel=1
pkgdesc="FlightAware fork of dump1090, a simple Mode S decoder for RTLSDR devices."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/flightaware/dump1090_mr"
license=('BSD')
depends=('rtl-sdr')
makedepends=('git')
source=('dump1090_mr::git+git://github.com/flightaware/dump1090_mr')
md5sums=('SKIP')
 
pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_gitname}"
  sed -i '6,7d' Makefile
}

build() {
  cd "${srcdir}/${_gitname}"
  make PREFIX=/usr/
}
 
package() {
  # mkdir -p "${pkgdir}/usr/bin"
  install -D -m755 "${srcdir}/${_gitname}/dump1090" "${pkgdir}/usr/bin/dump1090"
  install -D -m755 "${srcdir}/${_gitname}/view1090" "${pkgdir}/usr/bin/view1090"

  mkdir -p "${pkgdir}/usr/share"
  cp -r "${srcdir}/${_gitname}/public_html/" "${pkgdir}/usr/share/${_gitname}/"
}
